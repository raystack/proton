apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group 'io.odpf'
version '0.2.0'

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

sourceSets {
    main {
        java {
            srcDir "$projectDir/build/generated/main/java"
        }
        proto {
            srcDir "."
            exclude "build/**"
            exclude ".idea/**"
            exclude ".gradle/**"
            exclude ".github/**"
            exclude "docs/**"
        }
    }
}


buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}


dependencies {
    compile 'com.google.protobuf:protobuf-java:3.11.4'

    if (JavaVersion.current().isJava9Compatible()) {
        compile 'javax.annotation:javax.annotation-api:1.3.1'
    }
}


protobuf {
    generatedFilesBaseDir = "$projectDir/build/generated"
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.0'
    }
    plugins {}
    generateProtoTasks {
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.includeSourceInfo = true
            task.descriptorSetOptions.includeImports = true
            task.descriptorSetOptions.path = "$projectDir/build/descriptors/${project.version}"


            // incase required to generate for multiple languages
            // refer to gradle protobuf plugin documentation: https://github.com/google/protobuf-gradle-plugin
            // python{}
            // go{}
        }
    }
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/odpf/proton"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
